
add_library(platform_config INTERFACE)

if(CMAKE_SIZEOF_VOID_P MATCHES "8")
    set(PLATFORM_64BIT ON)
    target_compile_definitions(platform_config INTERFACE -DPLATFORM_64BITS=1) # -DX64BITS=1
    message(STATUS "Building for 64 Bit")
else()
    set(PLATFORM_64BIT OFF)
    message(STATUS "Building for 32 Bit")
endif()

if(ANDROID)
    message(STATUS "Building for Android")
    set(LINUX ON)
    target_compile_definitions(platform_config INTERFACE -D_ANDROID -DANDROID)
    target_compile_definitions(platform_config INTERFACE -D_LINUX -DLINUX)
    set(PLATFORM_LIBRARY_STATIC_EXT ".a")
    set(PLATFORM_LIBRARY_SHARED_EXT ".so")
    set(PLATFORM_LIBRARY_PREFIX "lib")
elseif(IOS)
    message(STATUS "Building for iOS")
    set(OSX ON)
    target_compile_definitions(platform_config INTERFACE -DOSX -D_OSX)
    target_compile_definitions(platform_config INTERFACE -DIOS)
    set(PLATFORM_LIBRARY_STATIC_EXT ".a")
    set(PLATFORM_LIBRARY_SHARED_EXT ".dylib")
    set(PLATFORM_LIBRARY_PREFIX "lib")
elseif(CMAKE_SYSTEM_NAME MATCHES "Darwin")
    message(STATUS "Building for macOS")
    set(OSX ON)
    target_compile_definitions(platform_config INTERFACE -DOSX -D_OSX)
    set(PLATFORM_LIBRARY_STATIC_EXT ".a")
    set(PLATFORM_LIBRARY_SHARED_EXT ".dylib")
    set(PLATFORM_LIBRARY_PREFIX "lib")
elseif(CMAKE_SYSTEM_NAME MATCHES "Linux")
    message(STATUS "Building for Linux")
    set(LINUX ON)
    target_compile_definitions(platform_config INTERFACE -D_LINUX -DLINUX)
    set(PLATFORM_LIBRARY_STATIC_EXT ".a")
    set(PLATFORM_LIBRARY_SHARED_EXT ".so")
    set(PLATFORM_LIBRARY_PREFIX "lib")
elseif(WIN32)
    message(STATUS "Building for Windows")
    set(PLATFORM_LIBRARY_STATIC_EXT ".lib")
    set(PLATFORM_LIBRARY_SHARED_EXT ".dll")
    set(PLATFORM_LIBRARY_STATIC_SUFFIX "")
endif()

target_compile_definitions(platform_config INTERFACE "-D_DLL_EXT=${PLATFORM_LIBRARY_SHARED_EXT}")

if(NOT WIN32)
    set(POSIX ON)
endif()

if(POSIX)
    target_compile_definitions(platform_config INTERFACE -DPOSIX -D_POSIX)
endif()

if(MSVC)
    target_compile_options(platform_config INTERFACE /wd4819 /wd4828 /wd5033 /permissive- /utf-8)
    target_compile_definitions(platform_config INTERFACE -D_CRT_SECURE_NO_WARNINGS=1)
elseif(${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
    target_compile_options(platform_config INTERFACE -Wno-register -Wno-dynamic-exception-spec -Wno-implicit-float-conversion -Wno-deprecated-volatile -Wno-inconsistent-missing-override -Wno-invalid-offsetof -Wno-null-dereference)
    target_compile_options(platform_config INTERFACE -fvisibility=hidden)
    target_compile_definitions(platform_config INTERFACE -DGNUC)
elseif(${CMAKE_CXX_COMPILER_ID} MATCHES "GNU")
    target_compile_options(platform_config INTERFACE -Wno-register -Wno-narrowing -Wno-volatile)
    target_compile_options(platform_config INTERFACE -fvisibility=hidden)
    target_compile_definitions(platform_config INTERFACE -DGNUC)
endif()

if(CMAKE_BUILD_TYPE MATCHES "Debug")
    target_compile_definitions(platform_config INTERFACE -DDEBUG=1 -D_DEBUG=1)
else()
    target_compile_definitions(platform_config INTERFACE -DNDEBUG=1)
endif()

if(OSX OR ANDROID)
    # Newer macOS forbids this
    target_compile_definitions(platform_config INTERFACE -DNO_HOOK_MALLOC -DNO_MALLOC_OVERRIDE)
endif()

target_compile_definitions(platform_config INTERFACE -DSTATIC_TIER0 -DSTATIC_VSTDLIB)